plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'java'
    id "io.codearte.nexus-staging" version "0.21.1"
}
group = 'io.github.atmaramnaik'
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    group = 'io.github.atmaramnaik'
    project.ext {
        apiVersion ='0.0.2'
        publishing = false
    }
    repositories {
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'signing'
    apply plugin: 'maven'
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    lombok { // optional: values below are the defaults
        version = '1.18.4'
        sha256 = ""
    }
    dependencies {
        implementation "org.projectlombok:lombok:1.18.6"
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.27.0'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
    if(this.archivesBaseName.startsWith('journey')){
        signing {
            useGpgCmd()
            sign configurations.archives
        }
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }
        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
        artifacts {
            archives javadocJar, sourcesJar
        }

        // Build, sign, and upload
        uploadArchives {
            repositories {
                mavenDeployer {

                    // Sign POM
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    // Destination
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    // Add required metadata to POM
                    pom.project {
//                        name projectName
                        packaging 'jar'
//                        description projectDesc
                        url 'https://atmaramnaik.github.io/journey'

                        scm {
                            connection 'scm:git:https://github.com/atmaramnaik/journey-code.git'
                            developerConnection 'scm:git:https://github.com/atmaramnaik/journey-code.git'
                            url 'scm:git:https://github.com/atmaramnaik/journey-code.git/master'
                        }

                        licenses {
                            license {
                                name 'The Apache License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id 'atmaramnaik'
                                name 'Atmaram Naik'
                                email 'naik_atmaram@yahoo.com'
                                organization 'Thoughtworks'
                                organizationUrl 'https://www.thoughtworks.com/'
                            }
                        }
                    }
                }
            }
        } 
    }
}
nexusStaging {
    username = ossrhUsername
    password = ossrhPassword
}
